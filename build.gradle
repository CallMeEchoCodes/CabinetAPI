plugins {
    id "java"
    id "fabric-loom" version "1.5-SNAPSHOT"
    id "maven-publish"

    id "com.replaymod.preprocess" version "ce1aeb2"
}

preprocess {
    def minecraft_1_20_1 = createNode("1.20.1", 1_20_01, "")
    def minecraft_1_20_2 = createNode("1.20.2", 1_20_02, "")
    def minecraft_1_20_3 = createNode("1.20.3", 1_20_03, "")
    def minecraft_1_20_4 = createNode("1.20.4", 1_20_04, "")

    minecraft_1_20_1.link(minecraft_1_20_2, null)
    minecraft_1_20_2.link(minecraft_1_20_3, null)
    minecraft_1_20_3.link(minecraft_1_20_4, null)
}


tasks.register("buildAndGather") {
    subprojects {
        dependsOn project.tasks.named("build").get()
    }

    doFirst {
        println("Gathering builds...")

        def buildLibs = {
            file -> file.buildDir.toPath().resolve("libs")
        }

        delete fileTree(buildLibs(rootProject)) {
            include "*"
        }

        subprojects {
            copy {
                from(buildLibs(project)) {
                    include "*.jar"
                    exclude "*-sources.jar"
                    exclude "*-javadoc.jar"
                    exclude "*-dev.jar"
                }

                into buildLibs(rootProject)
                duplicatesStrategy = DuplicatesStrategy.INCLUDE
            }
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.root_minecraft_version}"
    mappings "net.fabricmc:yarn:${project.root_yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.root_fabric_version}"
}